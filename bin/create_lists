#!/usr/bin/perl

use strict;
use warnings;
use 5.010;
use lib 'lib';

use DateTime;
use Scalar::Util qw[blessed];
use Getopt::Long;
use Log::Log4perl qw[:easy];
use Try::Tiny;
use Sys::RunAlone;

use TwittElection::Twitter;
use TwittElection::Schema;

my $verbose;
my $force = 0;
GetOptions(verbose => \$verbose,
           force   => \$force);

my $log_options = {
  level => $INFO,
  utf8  => 1,
};

$log_options->{level} = $TRACE if $verbose;

Log::Log4perl->easy_init($log_options);

my $logger = Log::Log4perl->get_logger;

my $sch = TwittElection::Schema->get_schema;

my $con_rs;

my $delay = 0;

if (@ARGV) {
  $con_rs = $sch->resultset('Constituency')->search({
    mapit_id => \@ARGV,
  });
  $delay = 0;
  $force = 1;
} else {
  $con_rs = $sch->resultset('Constituency');
}

my $t = TwittElection::Twitter->new(
  traits          => [ 'API::RESTv1_1', 'OAuth' ],
  ssl             => 1,
  consumer_key    => $ENV{TE_TW_API_KEY},
  consumer_secret => $ENV{TE_TW_API_SEC},
);

$t->authorise;

# Here's how this should work.
#
# IF constit has a list id and that list exists
#   Delete all members
# ELSE
#   Create new list
#   Update list name and list id
# END
#
# Insert list members

# Twitter rate limits (https://dev.twitter.com/rest/public/rate-limits)
# show_list : 15 / 15 min
# list_members : 15 / 15 min
# remove_list_members :
# create_list : 
# add_list_member : 

# Some thoughts about a more intelligent approach.
# Add a 'list_updated_timestamp' column to constituency table.
# Only attempt to rebuild list if
#   candidates_updated timestamp > list_updated_timestamp
# Get current list members and current candidate list.
# Only attempt to rebuild Twitter list if these lists differ.
# Update list_updated_timestamp when we update Twitter list.
# 
# Also, need to think about Twitter rate limiting.
# Stop working as soon as we get a rate exceeded response.

foreach my $con ($con_rs->all) {
  $logger->info($con->name . ' (' . $con->mapit_id . ')');

  unless ($force or $con->candidates->count) {
    $logger->info('No candidates found');
    next;
  }

  unless ($force) {
    if ($con->candidates_updated_time <= $con->list_rebuilt_time) {
      $logger->info('No need to rebuild list');
      next;
    }

    if ($delay and $con->list_rebuilt_time > DateTime->now->subtract(days=>1)) {
      $logger->info('List rebuilt too recently');
      next;
    }
  }

  my $list;
  try {
    if ($con->list_id) {
      $logger->info('Looking for list ', $con->list_name);
      $list = $t->show_list({
        owner_screen_name => 'twittelection',
        slug              => $con->list_name,
      });
      if ($list) {
        $logger->info("Found list: $list->{id}");
        foreach my $mem (@{$t->list_members({ list_id => $list->{id} })->{users}}) {
          $logger->info("Removing list member: $mem->{screen_name}");
          $t->remove_list_members({
            list_id     => $list->{id},
            screen_name => $mem->{screen_name},
          });
        }
      } else {
        $logger->info('Create ' . $con->name);
        $list = $t->create_list({
          owner_screen_name => 'twittelection',
          description       => $con->name,
          name              => $con->abbrev_name,
        });

        $logger->info($list->{slug});

        $con->update({
          list_name => $list->{slug},
          list_id   => $list->{id},
        });
      }
    } else {
      $logger->info('Create ' . $con->name);
      $list = $t->create_list({
        owner_screen_name => 'twittelection',
        description       => $con->name,
        name              => $con->abbrev_name,
      });

      $logger->info($list->{slug});

      # Remove the number that Twitter will sometimes add at the end
      $list->{slug} =~ s/-\d+$//;

      $con->update({
        list_name => $list->{slug},
        list_id   => $list->{id},
      });
    }

    foreach my $cand ($con->candidates) {
      next unless $cand->twitter;
      $logger->info("$list->{id} -> " . $cand->name . ' / ' . $cand->twitter);

      try {
        $t->add_list_member({
          list_id     => $list->{id},
          screen_name => $cand->twitter,
        });
      } catch {
        my $err = $_;
        die $_ unless blessed $err and $err->isa('Net::Twitter::Error');
        die $_ unless $err->has_twitter_error;

        # 104 and 106 means the user has blocked you
        # 108 means you're trying to add a non-existent user to a list
        die $_ unless $err->twitter_error_code == 104 or
                      $err->twitter_error_code == 106 or
                      $err->twitter_error_code == 108;

        $logger->warn($err->twitter_error_code . ': ' .
                      $err->twitter_error_text);
      }
    }
    $con->update({
      list_rebuilt_time => DateTime->now,
    });
  } catch {
    my $err = $_;
    $logger->logdie($err)
      unless blessed $err and $err->isa('Net::Twitter::Error');
    $logger->warn($err->code . ': ' . $err->error . ' (' .
                  $err->message . ')');
    if ($err->has_twitter_error) {
      $logger->warn($err->twitter_error_code . ': ' .
                    $err->twitter_error_text);
    }
    if ($err->code == 429 or $err->code == 403) {
      $logger->error('Rate limit exceeded');
      exit;
    } else {
      return;
    }
  }
}

__END__
