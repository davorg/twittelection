#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use open IO => ':encoding(utf8)';

use DateTime;
use Getopt::Long;
use Log::Log4perl qw[:easy];
use Sys::RunAlone;
use Text::CSV_XS;
use LWP::Simple;

use lib 'lib';
use TwittElection::Schema;

my $election_id   = 'parl.2017-06-08';

my $verbose;
GetOptions(verbose => \$verbose);

my $log_options = {
  level => $INFO,
  utf8  => 1,
};

$log_options->{level} = $TRACE if $verbose;

Log::Log4perl->easy_init($log_options);

my $logger = Log::Log4perl->get_logger;

my $sch = TwittElection::Schema->get_schema;

my $csv_file = 'candidates-parl.2017-06-08.csv';
my $csv_url =  "https://candidates.democracyclub.org.uk/media/$csv_file";

getstore($csv_url, $csv_file);
open my $csv_fh, '<', $csv_file or die $!;

my $csv = Text::CSV_XS->new({ binary => 1, auto_diag => 1 });

my %constits;

$csv->getline($csv_fh); # skip headers

while (my $row = $csv->getline($csv_fh)) {
  my $constit_id = $row->[9];
  $constit_id =~ s/^WMC://;
  $constits{$constit_id}{name} = $row->[10];
  my $cand;
  $cand->{id} = $row->[0];
  $cand->{name} = $row->[1];
  $cand->{party}{id} = $row->[7];
  $cand->{party}{name} = $row->[8];
  $cand->{twitter} = $row->[14];

  push @{ $constits{$constit_id}{cands} }, $cand;
}

my $cons_rs;
if (@ARGV) {
  $cons_rs = $sch->resultset('Constituency')->search({
    mapit_id => \@ARGV,
  });
} else {
  $cons_rs = $sch->resultset('Constituency');
}

my $par_rs = $sch->resultset('Party');
my $can_rs = $sch->resultset('Candidate');

foreach my $con ($cons_rs->all) {
  my $changed = 0;
  $logger->info($con->name . ' (' . $con->mapit_id . ')');

  my $cands = $constits{$con->demclub_id}{cands};

  my %curr_cands = map { $_->yournextmp_id => $_ } $con->candidates;

  my %cands;
  for (@$cands) {
    $cands{$_->{id}} = $_;
  }

  for (values %cands) {

    my ($party_id) = $_->{party}{id} =~ /(\d+)/;
    my $party;
    unless ($party = $par_rs->find({ yournextmp_id => $party_id })) {
      $party = $par_rs->create({
        name => $_->{party}{name},
        yournextmp_id => $party_id,
      });
    }

    if (exists $curr_cands{$_->{id}}) {
      my $curr_cand = delete $curr_cands{$_->{id}};
      if ($_->{name} ne $curr_cand->name
        or $_->{twitter} ne $curr_cand->twitter) {
        $logger->info("* Updating $_->{name} / " .
            $_->{party}{name} .
            " / \@$_->{twitter}");
        $curr_cand->update({
          yournextmp_id   => $_->{id},
          name            => $_->{name},
          twitter         => $_->{twitter},
          party_id        => $party->id,
          constituency_id => $con->id,
        });
        $changed = 1;
      } else {
        $logger->trace("* No change $_->{name} / " .
            $_->{party}{name} .
            " / \@$_->{twitter}");
      }
    } else {
      $logger->info("* Adding $_->{name} / " .
          $_->{party}{name} .
          " / \@$_->{twitter}");
      # Note: We use update_or_create here instead of create in order to
      # deal with the situation where a candidate moves from one
      # constituency to another.
      $can_rs->update_or_create({
        yournextmp_id   => $_->{id},
        name            => $_->{name},
        twitter         => $_->{twitter},
        party_id        => $party->id,
        constituency_id => $con->id,
      });
      $changed = 1;
    }
  }

  # Any candidates left in %curr_cands need to be deleted
  foreach (keys %curr_cands) {
    my $curr_cand = $curr_cands{$_};
    $logger->info('* Deleting ' . $curr_cand->name . ' / ' .
        $curr_cand->party->name .
        ' / ' . $curr_cand->twitter);
    $curr_cand->delete;
    $changed = 1;
  }

  if ($changed) {
    $con->update({
      candidates_updated_time => DateTime->now,
    });
  }
}

__END__
