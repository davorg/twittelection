#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use open IO => ':encoding(utf8)';

use DateTime;
use Getopt::Long;
use Sys::RunAlone;
use Text::CSV_XS;
#use Carp::Always;

use lib 'lib';
use TwittElection::App;
use TwittElection::Util 'partition_hash';

my $verbose;
GetOptions(verbose => \$verbose);

my $app = TwittElection::App->new({ verbose => $verbose });

my %constits;

# Skip header
$app->get_data_line;

while (my $row = $app->get_data_line) {
  my $constit_id = $row->[9];
  $constit_id =~ s/^WMC://;
  $constits{$constit_id}{name} = $row->[10];
  my $cand;
  $cand->{id} = $row->[0];
  $cand->{name} = $row->[1];
  $cand->{party}{id} = $row->[7];
  $cand->{party}{name} = $row->[8];
  $cand->{twitter} = $row->[14];

  push @{ $constits{$constit_id}{cands} }, $cand;
}

my $cons_rs;
if (@ARGV) {
  $cons_rs = $app->constituency_rs->search({
    mapit_id => \@ARGV,
  });
} else {
  $cons_rs = $app->constituency_rs;
}

my $par_rs = $app->party_rs;
my $can_rs = $app->candidate_rs;

foreach my $con ($cons_rs->all) {
  # What we need to do here.
  # We have a constituency.
  # Get the candidates we have in the database.
  # Get the candidates we have in the new file.
  # Create three lists:
  # * Candidates to add to the database.
  # * Candidates to update in the database.
  # * Candidates to remove from the database.

  my $con_changed = 0;
  $app->logger->info($con->name . ' (' . $con->mapit_id . ')');

  my %file_cands = ();
  if ($constits{$con->demclub_id}{cands}) {
    %file_cands = map { $_->{id} => $_ } @{ $constits{$con->demclub_id}{cands} };
  }

  my %db_cands = map { $_->yournextmp_id => $_ } $con->candidates;

  unless (keys %file_cands or keys %db_cands) {
    $app->logger->info("No candidates in file or database. Skipping...");
    next;
  }

  my ($file_only, $both, $db_only) = partition_hash(\%file_cands, \%db_cands);

  for (values %$file_only) {
    my ($party_id) = $_->{party}{id} =~ /(\d+)/;
    my $party;
    unless ($party = $par_rs->find({ yournextmp_id => $party_id })) {
      $party = $par_rs->create({
        name => $_->{party}{name},
        yournextmp_id => $party_id,
      });
    }

    $app->logger->info("* Adding $_->{name} / " .
        $_->{party}{name} .
        " / \@$_->{twitter}");
    # Note: We use update_or_create here instead of create in order to
    # deal with the situation where a candidate moves from one
    # constituency to another.
    $can_rs->update_or_create({
      yournextmp_id   => $_->{id},
      name            => $_->{name},
      twitter         => $_->{twitter},
      party_id        => $party->id,
      constituency_id => $con->id,
    });
    $con_changed = 1;
  }

  for (values %$both) {
    my ($party_id) = $_->[0]{party}{id} =~ /(\d+)/;
    my $party;
    unless ($party = $par_rs->find({ yournextmp_id => $party_id })) {
      $party = $par_rs->create({
        name => $_->[0]{party}{name},
        yournextmp_id => $party_id,
      });
    }

    if ($_->[0]{name} ne $_->[1]->name
      or $_->[0]{twitter} ne $_->[1]->twitter) {
      $app->logger->info("* Updating $_->[0]{name} / " .
          $_->[0]{party}{name} .
          " / \@$_->[0]->{twitter}");
      $_->[1]->update({
        yournextmp_id   => $_->[0]{id},
        name            => $_->[0]{name},
        twitter         => $_->[0]{twitter},
        party_id        => $party->id,
        constituency_id => $con->id,
      });
      $con_changed = 1;
    }
  }

  for (values %$db_only) {
    $app->logger->info('* Deleting ' . $_->name . ' / ' .
        $_->party->name .
        ' / ' . $_->twitter);
    $_->delete;
    $con_changed = 1;
  }

  if ($con_changed) {
    $con->update({
      candidates_updated_time => DateTime->now,
      list_checked_time       => undef,
    });
    $app->has_changed(1);
  }
}

if ($app->has_changed) {
  $app->logger->info('Candidate updates received');
} else {
  $app->logger->info('No candidate updates received');
}

__END__
