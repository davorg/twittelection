#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use open IO => ':encoding(utf8)';

use DateTime;
use Getopt::Long;
use Sys::RunAlone;
use Text::CSV_XS;
#use Carp::Always;

use lib 'lib';
use TwittElection::App;

my $verbose;
GetOptions(verbose => \$verbose);

my $app = TwittElection::App->new({ verbose => $verbose });

my $election_id   = 'parl.' . $app->election_date->ymd;

my %constits;

# Skip header
$app->get_data_line;

while (my $row = $app->get_data_line) {
  my $constit_id = $row->[9];
  $constit_id =~ s/^WMC://;
  $constits{$constit_id}{name} = $row->[10];
  my $cand;
  $cand->{id} = $row->[0];
  $cand->{name} = $row->[1];
  $cand->{party}{id} = $row->[7];
  $cand->{party}{name} = $row->[8];
  $cand->{twitter} = $row->[14];

  push @{ $constits{$constit_id}{cands} }, $cand;
}

my $cons_rs;
if (@ARGV) {
  $cons_rs = $app->constituency_rs->search({
    mapit_id => \@ARGV,
  });
} else {
  $cons_rs = $app->constituency_rs;
}

my $par_rs = $app->party_rs;
my $can_rs = $app->candidate_rs;

foreach my $con ($cons_rs->all) {
  my $changed = 0;
  $app->logger->info($con->name . ' (' . $con->mapit_id . ')');

  my $cands = $constits{$con->demclub_id}{cands};

  my %curr_cands = map { $_->yournextmp_id => $_ } $con->candidates;

  unless ($cands or keys %curr_cands) {
    $app->logger->info("No candidates in file or database. Skipping...");
    next;
  }

  my %cands;
  for (@$cands) {
    $cands{$_->{id}} = $_;
  }

  for (values %cands) {

    my ($party_id) = $_->{party}{id} =~ /(\d+)/;
    my $party;
    unless ($party = $par_rs->find({ yournextmp_id => $party_id })) {
      $party = $par_rs->create({
        name => $_->{party}{name},
        yournextmp_id => $party_id,
      });
    }

    if (exists $curr_cands{$_->{id}}) {
      my $curr_cand = delete $curr_cands{$_->{id}};
      if ($_->{name} ne $curr_cand->name
        or $_->{twitter} ne $curr_cand->twitter) {
        $app->logger->info("* Updating $_->{name} / " .
            $_->{party}{name} .
            " / \@$_->{twitter}");
        $curr_cand->update({
          yournextmp_id   => $_->{id},
          name            => $_->{name},
          twitter         => $_->{twitter},
          party_id        => $party->id,
          constituency_id => $con->id,
        });
        $changed = 1;
      } else {
        $app->logger->trace("* No change $_->{name} / " .
            $_->{party}{name} .
            " / \@$_->{twitter}");
      }
    } else {
      $app->logger->info("* Adding $_->{name} / " .
          $_->{party}{name} .
          " / \@$_->{twitter}");
      # Note: We use update_or_create here instead of create in order to
      # deal with the situation where a candidate moves from one
      # constituency to another.
      $can_rs->update_or_create({
        yournextmp_id   => $_->{id},
        name            => $_->{name},
        twitter         => $_->{twitter},
        party_id        => $party->id,
        constituency_id => $con->id,
      });
      $changed = 1;
    }
  }

  # Any candidates left in %curr_cands need to be deleted
  foreach (keys %curr_cands) {
    my $curr_cand = $curr_cands{$_};
    $app->logger->info('* Deleting ' . $curr_cand->name . ' / ' .
        $curr_cand->party->name .
        ' / ' . $curr_cand->twitter);
    $curr_cand->delete;
    $changed = 1;
  }

  if ($changed) {
    $con->update({
      candidates_updated_time => DateTime->now,
      list_checked_time       => undef,
    });
    $app->has_changed(1);
  }
}

if ($app->has_changed) {
  $app->logger->info('Candidate updates received');
} else {
  $app->logger->info('No candidate updates received');
}

__END__
